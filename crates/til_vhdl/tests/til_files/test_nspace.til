namespace my::test::space {
    type byte = Bits(8);
    type select = Union(val: byte, empty: Null);
    type rgb = Group(r: select, g: select, b: select);
    type stream = Stream (
        data: rgb,
        throughput: 2.0,
        dimensionality: 0,
        synchronicity: Sync,
        complexity: 4,
        direction: Forward,
        user: Null,
        keep: false,
    );
    type stream2 = stream;

    streamlet comp1 = (
        a: in stream,
        b: out stream,
        c: in stream2,
        d: out stream2,
    );

    interface iface1 = (a: in stream, b: out stream);

    streamlet comp2 = iface1;

    impl struct = (
        a: in stream, // Ports are declared as *name* : *direction* *stream name*
        b: out stream,
        c: in stream2,
        d: out stream2,
    ){
        // Ports can be connected with --
        a -- b;

        // Streamlet instances are declared with
        // *instance name* = *streamlet name*
        a = comp1;
        b = comp1;

        // Considering c, d = comp;

        // Ports on streamlet instances can be addressed with .
        a.a--b.b;
        a.b-- b.a;
        // As with everything else, whitespace does not matter
        
        // Ports on streamlet instances can also be connected to local ports
        c -- a.c;
        d -- b.d;
        a.d -- b.c;
    };

    streamlet comp3 = comp1 {
        impl: {
            p1 = comp2;
            p2 = comp2;
            a -- p1.a;
            b -- p1.b;
            c -- p2.a;
            d -- p2.b;
        }
    };

    streamlet comp4 = comp1 {
        impl: struct
    };

    // Defining a streamlet as a copy of another streamlet (or of an import)
    // is partially optimized away during evaluation. Only the first declaration will be emitted.
    // TODO: Either go all the way (ensure that streamlets queried are also unique,
    //  as otherwise they're still being (over)written)
    // or do allow for copies (identical streamlets/implementations, with different names).
    streamlet comp5 = comp4;

    // Regardless, they can still be used as interfaces
    streamlet comp6 = comp5 {
        impl: struct,
    };

    // Final example, of the relative amount of work required to define a component:
    type my_stream = Stream(
        data: Bits(8),
        throughput: 5.0,
        dimensionality: 3,
        synchronicity: Sync,
        complexity: 7,
        direction: Forward, // Note, the keep and user properties are not required (nor is throughput, which defaults to 1)
    );
    streamlet my_new_streamlet = (
        in_port: in my_stream,
        out_port: out my_stream,
    ) {
        impl: {
            in_port -- out_port;
        },
    };
}